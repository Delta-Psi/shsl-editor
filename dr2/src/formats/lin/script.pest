WHITESPACE = _{ " " | "\t" }
COMMENT = _{
	("/*" ~ (!"*/" ~ ANY)* ~ "*/") |
	("//" ~ (!NEWLINE ~ ANY)*)
}

ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
int_hex = @{ "0x" ~ ASCII_HEX_DIGIT+ }
int_dec = @{ "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }

text = ${ "`\n" ~ text_inner ~ "`" }
text_inner = @{ text_char+ }
text_char = {
	(!("`" | "\\") ~ ANY) |
	("\\" ~ ("\\" | "t" | "`"))
}

arg = { ident | int_hex | int_dec | text }

instr = { ident ~ (arg ~ ("," ~ arg)*)? ~ "\n" }

script = { SOI ~ instr* ~ EOI }
